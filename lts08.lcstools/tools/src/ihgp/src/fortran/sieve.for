C
C SIEVE OF ERASTOSTHENES FOR GENERATING PRIME NUMBERS.
C
C        1         2         3
C23456789012345678901234567890123456789
C
      PROGRAM SIEVE
C
C DECLARE PARAMETERS
C
      INTEGER MAXPRIME
      PARAMETER(MAXPRIME=1000)
C
C LOCAL VARIABLES.
C
      INTEGER I, J, USERMX
      LOGICAL PRIME(MAXPRIME)
C
C GET MAXIMUM FROM USER AND VERIFY IT IS WITHIN RANGE.
C
 50   PRINT *, 'ENTER MAXIMUM PRIME TO GENERATE'
      READ *, USERMX
      IF (USERMX.LE.1.OR.USERMX.GT.MAXPRIME) GOTO 50
C
C ASSUME ALL NUMBERS ARE PRIME.
C
      DO 100 I=1, USERMX, 1
          PRIME(I) = .TRUE.
 100  CONTINUE
C
C REMOVE ALL MULTIPLES OF NUMBERS THAT ARE KNOWN TO BE PRIME.
C
      DO 300 I=2, USERMX, 1
          IF (PRIME(I)) THEN
              PRINT *, 'PRIME NUMBER = ', I
              DO 200 J=2*I, USERMX, I
                  PRIME(J) = .FALSE.
 200          CONTINUE
          ENDIF
 300  CONTINUE
C
C ALL DONE
C
      END
